apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def BOOLEAN = "boolean"
def STRING = "String"
def TRUE = "true"
def FALSE = "false"
def LOG_HTTP_REQUESTS = "LOG_HTTP_REQUESTS"
def REPORT_CRASHES = "REPORT_CRASHES"
def API_URL = "API_URL"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {

        applicationId "info.marsic.ktboilerplate"

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        proguardFile getDefaultProguardFile('proguard-android.txt')
        proguardFiles fileTree(include: ['*.pro'], dir: 'proguard').asList().toArray()

        vectorDrawables {
            useSupportLibrary = true
        }

        buildConfigField STRING, API_URL, '"http://API_URL/"'
        buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
        buildConfigField BOOLEAN, REPORT_CRASHES, FALSE

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG_KEYSTORE_PATH)
            storePassword DEBUG_KEYSTORE_PASS
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASS
        }

        release {
            storeFile file(RELEASE_KEYSTORE_PATH)
            storePassword RELEASE_KEYSTORE_PASS
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASS
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            debuggable true
            minifyEnabled false
            shrinkResources false

            testCoverageEnabled = TRUE
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError true
        disable 'InvalidPackage'
    }

}

tasks.whenTaskAdded { task ->
    if (task.name == "lint") {
        task.enabled = false
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"

    implementation "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"

    // Dagger - dependency injection
    implementation "com.google.dagger:dagger:${daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"

    // rest client for http interaction and dependencies
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"

    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"

    // Kotlin
    testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlinVersion}"

    // Unit testing dependencies
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${hamcrestVersion}"

    testImplementation("com.squareup.assertj:assertj-android:${assertjVersion}") {
        exclude module: "support-annotations"
    }

    kaptAndroidTest "com.google.dagger:dagger-compiler:${daggerVersion}"

    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
    androidTestImplementation "com.android.support.test:runner:${runnerVersion}"

    // Set this dependency to use JUnit 4 rules
    androidTestImplementation "com.android.support.test:rules:${rulesVersion}"

    // Set this dependency to build and run UI Automator tests
    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"

    // Set this dependency to build and run Espresso tests
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoVersion}"

    androidTestImplementation "com.crittercism.dexmaker:dexmaker:${dexMakerVersion}"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:${dexMakerVersion}"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:${dexMakerVersion}"

    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }
}
